import dependencies.BuildPlugins

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath BuildPlugins.androidGradlePlugin
        classpath BuildPlugins.kotlinGradlePlugin
        classpath BuildPlugins.detektPlugin

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {

    apply from: "$rootDir/tools/ktlint.gradle"
    apply from: "$rootDir/tools/detekt.gradle"
    apply plugin: 'kotlin-android-extensions'

    afterEvaluate {
        if (!project.hasProperty("android")) return

        android {
            compileSdkVersion AndroidConfig.compileSdk

            defaultConfig {
                minSdkVersion AndroidConfig.minSdk
                targetSdkVersion AndroidConfig.targetSdk
                versionCode 01
                versionName "0.0.1"
                testInstrumentationRunner AndroidConfig.instrumentationTestRunner
            }

            compileOptions {
                sourceCompatibility 1.8
                targetCompatibility 1.8
            }

            androidExtensions {
                experimental = true
            }

            sourceSets {
                main.java.srcDirs += 'src/main/kotlin'
                test.java.srcDirs += 'src/test/kotlin'
                androidTest.java.srcDirs += 'src/androidTest/kotlin'
            }
        }


    }
}
